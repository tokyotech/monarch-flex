<?xml version="1.0" encoding="utf-8"?>
<mx:Window xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="creationCompleteHandler(event)" show="creationCompleteHandler(event)" close="closingHandler(event)" layout="absolute" width="444" height="440" showStatusBar="false" showInAutomationHierarchy="false" showGripper="false" showTitleBar="false" maxHeight="440" maxWidth="444" minHeight="440" minWidth="444" disabledOverlayAlpha=".7" backgroundDisabledColor="#000000" horizontalScrollPolicy="off">

	<mx:Metadata>
		[Event(name="CreateWebsiteEvent", type="cCreateWebsiteEvent")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
		
			///////////////////////////////////////////////////////////////////////////////////
			//
			//								EVENT HANDLERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 * 	Event handler which is triggered by the user clicking the Create
			 * 	Website button.
			 */
			private function createWebsiteClickHandler(event:Event):void
			{
				if(mAddNewWebsiteRadioButton.selected) {
					createWebsiteProcess();
				}
				else if(mAddPrevWebsiteRadioButton.selected) {
					addWebsiteProcess();
				}
				
			}
			
			/**
			 *  Event handler triggered when the user clicks the ENTER keyboard
			 *  button while the cursor lies within the website name text box.
			 */
			private function websiteNameKeyDownHandler(event:KeyboardEvent):void
			{
				if(event.keyCode == Keyboard.ENTER) {
					createWebsiteProcess();
				}
			}
			
			/**
			 *  Event handler triggered when the user clicks the ENTER keyboard
			 *  button while the cursor lies within the website URL text box.
			 */
			private function websiteURLKeyDownHandler(event:KeyboardEvent):void
			{
				if(event.keyCode == Keyboard.ENTER) {
					createWebsiteProcess();
				}
			}
			
			/**
			 * 	Event handler which is triggered by the user clicking the Cancel button.
			 */
			private function cancelClickHandler(event:Event):void
			{
				cancelProcess();
			}
			
			/**
			 * 	Called when the user clicks on the add new website radio button.
			 */
			private function addNewWebsiteChangeHandler(event:Event):void
			{
				mAddNewWebsiteContainer.enabled = true;
				mAddPrevWebsiteContainer.enabled = false;	
				cCreateWebsiteButton.toolTip = "Create the website specified";
			}
			
			/**
			 * 	Called when the user clicks on the add previous website radio button.
			 */
			private function addPreviousWebsiteChangeHandler(event:Event):void
			{
				mAddNewWebsiteContainer.enabled = false;
				mAddPrevWebsiteContainer.enabled = true;	
				cCreateWebsiteButton.toolTip = "Add the previously setup website(s) selected";
			}
			

			///////////////////////////////////////////////////////////////////////////////////
			//
			//								MEMBER FUNCTIONS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 * 	Function which handles the processing of a create website event.
			 */
			private function createWebsiteProcess():void
			{
				if(mWebsiteNameTextBox.text.length == 0) {
					Alert.show("Please enter a website name before proceeding.", "Error", 1, this);
					return;
				}
					
				if(mWebsiteURLTextBox.text.length == 0) {
					Alert.show("Please enter a website URL before proceeding.", "Error", 1, this);
					return;
				}
				
				var e:cCreateWebsiteEvent = new cCreateWebsiteEvent("cCreateWebsiteEvent");
				
				var websiteType:String = "";
				if(mForumTypeRadioButton.selected) {
					websiteType = e.WEBSITE_TYPE_FORUM;
				}
				else if(mBlogTypeRadioButton.selected) {
					websiteType = e.WEBSITE_TYPE_BLOG;
				}
				else if(mNewsTypeRadioButton.selected) {
					websiteType = e.WEBSITE_TYPE_NEWS;
				}
				
				if(websiteType.length == 0) {
					Alert.show("Please select a type for the website before proceeding.", "Error", 1, this);
					return;
				}
				
				e.mType = e.CREATE_WEBSITE;
				e.mWebsiteType = websiteType;
				e.mWebsiteName = mWebsiteNameTextBox.text;
				e.mWebsiteURL = mWebsiteURLTextBox.text;
				e.mCommunityGroupId = mCommunityGroupId;
				this.dispatchEvent(e);
				this.close();
			}
			
			/**
			 *  Function which handles the processing of a add website(s) event.
			 */
			private function addWebsiteProcess():void
			{
				
			}
			
			/**
			 * 	Function which handles the processing of a cancel event.
			 */
			private function cancelProcess():void
			{
				var e:cCreateWebsiteEvent = new cCreateWebsiteEvent("cCreateWebsiteEvent");
				e.mType = e.CANCEL;
				this.dispatchEvent(e);
				this.close();
			}
			
			/**
			 * 	Function which updates the data members for this window.
			 */
			public function updateInformation(communityGroupId:int):void
			{
				mCommunityGroupId = communityGroupId;
			}
			
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//								WINDOW EVENT HANDLERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 * 	Event handler called when the window is first created.
			 */
			private function creationCompleteHandler(event:Event):void
			{
				mWebsiteNameTextBox.setFocus();
				mAddPrevWebsiteContainer.enabled = false;
				mAddNewWebsiteContainer.enabled = true;
				cCreateWebsiteButton.toolTip = "Create the website specified";
			}
			
			/**
			 * 	Event handler triggered when the window is about to close.
			 */
			private function closingHandler(event:Event):void
			{
				cancelProcess();
			}
			
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//								DATA MEMBERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			private var mCommunityGroupId:int = -1;
			
		]]>
	</mx:Script>

	<mx:Label y="10" text="Add Website Creator" fontSize="14" id="mCreateWebsiteLabel" textDecoration="underline" horizontalCenter="0"/>
	<mx:Button click="createWebsiteClickHandler(event)" id="cCreateWebsiteButton" label="Add Website(s)" horizontalCenter="153" verticalCenter="198"/>
	<mx:Button click="cancelClickHandler(event)" id="mCancelButton" label="Cancel" toolTip="Cancel the create account aperation" horizontalCenter="56" verticalCenter="198"/>
	<mx:Canvas width="418" height="131" id="mAddNewWebsiteContainer"   horizontalCenter="2" verticalCenter="-91" enabled="true" backgroundDisabledColor="#000000" disabledOverlayAlpha=".7">
		<mx:Label x="7.5" y="10" text="Please enter the name, URL, and select the type of website you want  " color="#F0F0F0"/>
		<mx:Label x="7.5" y="25" text="to create - then click Add Website." color="#F0F0F0"/>
		<mx:Label x="7.5" y="53" text="Website Name :" keyDown="websiteNameKeyDownHandler(event)" color="#F0EEEE"/>
		<mx:Label x="7.5" y="79" text="Website URL :" keyDown="websiteURLKeyDownHandler(event)" color="#F0EEEE"/>
		<mx:Label x="7.5" y="105" text="Website Type :" color="#F0EEEE"/>
		<mx:TextInput x="110.5" y="51" width="296" id="mWebsiteNameTextBox"/>
		<mx:TextInput x="110.5" y="77" width="296" id="mWebsiteURLTextBox"/>
		<mx:RadioButton x="110.5" y="103" label="Forum" textRollOverColor="#11D30C" textSelectedColor="#FF6666" id="mForumTypeRadioButton"/>
		<mx:RadioButton x="178.5" y="103" label="Blog" textRollOverColor="#11D30C" textSelectedColor="#FF6666" id="mBlogTypeRadioButton"/>
		<mx:RadioButton x="236" y="103" label="News" textRollOverColor="#11D30C" textSelectedColor="#FF6666" id="mNewsTypeRadioButton"/>
	</mx:Canvas>
	<mx:RadioButton label="Add New Website" change="addNewWebsiteChangeHandler(event)" selected="true" textRollOverColor="#B7BABC" textSelectedColor="#B7BABC" id="mAddNewWebsiteRadioButton" x="10" verticalCenter="-175">
		<mx:toolTip>Create and add a website which has not been previously created</mx:toolTip>
	</mx:RadioButton>
	<mx:RadioButton x="14" label="Add Previously Setup Websites" change="addPreviousWebsiteChangeHandler(event)" textRollOverColor="#B7BABC" textSelectedColor="#B7BABC" id="mAddPrevWebsiteRadioButton" verticalCenter="-6">
		<mx:toolTip>Add a website which has been previously created and added</mx:toolTip>
	</mx:RadioButton>
	<mx:VBox height="166" width="418" enabled="false" disabledOverlayAlpha=".7" id="mAddPrevWebsiteContainer" alpha="1" horizontalCenter="2" verticalCenter="96" backgroundDisabledColor="#000000" verticalScrollPolicy="on" horizontalScrollPolicy="off">
	</mx:VBox>
	
</mx:Window>
