<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="showCanvasHandler(event)" show="showCanvasHandler(event)" width="993" height="567" backgroundColor="#FFFFFF" backgroundAlpha="0.1">
	
	<mx:Metadata>
		[Event(name="OpenCreateCommunityGroupCanvasEvent", type="flash.events.Event")]
		[Event(name="OpenCommunityGroupBrowserCanvasEvent", type="cCommGroupGraphicClickedEvent")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.effects.Fade;
			import mx.effects.Move;
			import mx.effects.Tween;
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//							GUI EVENT HANDLERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 * 	Fires an event signaling that the user has clicked the create
			 *  community group label.
			 */
			private function createCommClickHandler(event:Event):void
			{
				var e:Event = new Event("OpenCreateCommunityGroupCanvasEvent");
				this.dispatchEvent(e);
			}
			
			/**
			 * 	Called when the user hovers the mouse over the create community group label.
			 */
			private function createCommRollOverHandler(event:Event):void
			{
				mCreateCommLabel.setStyle("color", "0xFF6666");
			}
			
			/**
			 * 	Called when the user hovers the mouse away from the create community group label.
			 */
			private function createCommRollOutHandler(event:Event):void
			{
				mCreateCommLabel.setStyle("color", "0x11d30c");
			}
			
			/**
			 * 	Fires an event signaling that the user has clicked the remove
			 *  community groups label.
			 */
			private function removeCommClickHandler(event:Event):void
			{
				if(mRemoveCommGroupLabelEnabled) {
					var i:int = 0;
					for(i; i < mMyCommVBox.getChildren().length; i++) {
						if(mMyCommVBox.getChildAt(i) is cCommGroupLargeGraphic) {
							var commGroupGraphic:cCommGroupLargeGraphic = cCommGroupLargeGraphic(mMyCommVBox.getChildAt(i));
							commGroupGraphic.showRemoveCommGroupsLabel();
						}
					}
					mMyCommVBox.setStyle("backgroundColor", "0x000000");
					
					// animate
					var move:Move = new Move();
					move.xFrom = mRemoveCommGroupsLabel.x;
					move.xTo = mRemoveCommGroupsLabel.x - 30;
					move.target = mRemoveCommGroupsLabel;
					move.duration = 500;
					move.play();
					
					mSmallXLabel.visible = true;
					var fade:Fade = new Fade();
					fade.alphaFrom = 0;
					fade.alphaTo = 1;
					fade.duration = 500;
					fade.target = mSmallXLabel;
					fade.play();
					
					mRemoveCommGroupsLabel.enabled = false;
					mRemoveCommGroupLabelEnabled = false;
				}
			}
			
			/**
			 * 	Fires an event signaling that the user has clicked the small X
			 * 	label.
			 */
			private function smallXClickHandler(event:Event):void
			{
				var i:int = 0;
					for(i; i < mMyCommVBox.getChildren().length; i++) {
						if(mMyCommVBox.getChildAt(i) is cCommGroupLargeGraphic) {
							var commGroupGraphic:cCommGroupLargeGraphic = cCommGroupLargeGraphic(mMyCommVBox.getChildAt(i));
							commGroupGraphic.hideRemoveWebsiteLabel();
						}
					}
					mMyCommVBox.setStyle("backgroundColor", "0xffffff");
					
					// animate
					var move:Move = new Move();
					move.xFrom = mRemoveCommGroupsLabel.x;
					move.xTo = mRemoveCommGroupsLabel.x + 30;
					move.target = mRemoveCommGroupsLabel;
					move.duration = 500;
					move.play();
						
					var fade:Fade = new Fade();
					fade.alphaFrom = 1;
					fade.alphaTo = 0;
					fade.duration = 500;
					fade.target = mSmallXLabel;
					fade.play();
					mSmallXLabel.visible = false;
					
					mRemoveCommGroupsLabel.enabled = true;
					mRemoveCommGroupLabelEnabled = true;
			}
			
			/**
			 * 	Called when the user hovers the mouse over the remove community group label.
			 */
			private function removeCommRollOverHandler(event:Event):void
			{
				mRemoveCommGroupsLabel.setStyle("color", "0xFF6666");
			}
			
			/**
			 * 	Called when the user hovers the mouse away from the remove community group label.
			 */
			private function removeCommRollOutHandler(event:Event):void
			{
				mRemoveCommGroupsLabel.setStyle("color", "0x11d30c");
			}
			
			/**
			 *  Event handler called when the user clicks on the community group graphic whithin this canvas.
			 * 	Fires an event signaling that a community has been selected by the user.
			 */
			private function commGroupGraphicClickedHandler(event:cCommGroupGraphicClickedEvent):void
			{
				var i:int
				var communityGroupEntry:cCommunityGroupEntry;
				for(i = 0; i < mUserAccountInformation.mCommunities.length; i++) {
					communityGroupEntry = cCommunityGroupEntry(mUserAccountInformation.mCommunities.getItemAt(i));
					if(communityGroupEntry.mId == event.mCommGroupId) {
						var e:cCommGroupGraphicClickedEvent = new cCommGroupGraphicClickedEvent("OpenCommunityGroupBrowserCanvasEvent");
						e.mCommGroupName = event.mCommGroupName;
						e.mCommGroupId = event.mCommGroupId;
						e.mCreator = mUserAccountInformation.mUserName;
						e.mWebsites = communityGroupEntry.mWebsites;
						this.dispatchEvent(e);
						return;
					}
				}
			}
			
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//							CANVAS EVENT HANDLERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 *  Called when this canvas has been opened.
			 */
			private function showCanvasHandler(event:Event):void
			{		
				
			}
			
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//							 MEMBER FUNCTIONS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			/**
			 *  Function which updates the users information within this canvas component to be used
			 *  at a later time. Also displays the graphics for this canvas component.
			 */
			public function updateInformation(userAccountInformation:cAccountInformation):void
			{
				mUserAccountInformation = userAccountInformation;
				
				mMyCommVBox.removeAllChildren();
				
				var delay:int = 100;
				var i:int;
				var communityGroupEntry:cCommunityGroupEntry;
				for(i = 0; i < mUserAccountInformation.mCommunities.length; i++) {
					communityGroupEntry = cCommunityGroupEntry(mUserAccountInformation.mCommunities.getItemAt(i));
					var commGroupGraphicEntry:cCommGroupLargeGraphic = new cCommGroupLargeGraphic();
					commGroupGraphicEntry.addEventListener("cCommGroupGraphicClickedEvent", commGroupGraphicClickedHandler);
					var justCreated:Boolean = false;
					if(communityGroupEntry.mJustCreated) {
						justCreated = true;
						communityGroupEntry.mJustCreated = false;
					}
					commGroupGraphicEntry.updateInformation(communityGroupEntry.mName,
															mUserAccountInformation.mUserName,
															"",
															communityGroupEntry.mWebsites.length.toString(),
															communityGroupEntry.mId,
															justCreated);								
					commGroupGraphicEntry.alpha = 0.3;					
					mMyCommVBox.addChild(commGroupGraphicEntry);
					// animate
					var fade:Fade = new Fade();
					fade.alphaFrom = 0.3;
					fade.alphaTo = 0.8;
					fade.duration = 500;
					fade.startDelay = delay;
					fade.target = commGroupGraphicEntry;
					fade.play();
					
					delay+=200;	
				}	
			}
			
			
			///////////////////////////////////////////////////////////////////////////////////
			//
			//							   DATA MEMBERS
			//
			///////////////////////////////////////////////////////////////////////////////////
			
			// reference to the users community group information object
			private var mUserAccountInformation:cAccountInformation = null;
			
			// variable used to enable and disable the remove website label
			private var mRemoveCommGroupLabelEnabled:Boolean = true;
			
		]]>
	</mx:Script>
	
	<mx:VRule x="447" y="10" height="547" strokeColor="#606060"/>
	<mx:Label x="20" y="10" text="My Community Groups" fontFamily="Times New Roman" fontSize="24" textDecoration="underline" color="#BEBEBE" alpha="1.0" id="mHomeLoggedOutAccountLabel"/>
	<mx:Label x="467" y="10" text="News" fontFamily="Times New Roman" fontSize="24" textDecoration="underline" color="#BEBEBE" alpha="1.0" id="mHomeLoggedOutAccountLabel0"/>
	<mx:Label x="283" y="21" text="Create Community Group" rollOver="createCommRollOverHandler(event)" rollOut="createCommRollOutHandler(event)" click="createCommClickHandler(event)" color="#13BB33" textAlign="center" textDecoration="underline" id="mCreateCommLabel"/>
	<mx:Label x="252" y="536" text="Remove Community Groups" rollOver="removeCommRollOverHandler(event)" rollOut="removeCommRollOutHandler(event)" click="removeCommClickHandler(event)" color="#13BB33" textAlign="center" textDecoration="normal" id="mRemoveCommGroupsLabel" fontSize="12"/>
	<mx:VBox x="20" y="50" height="478" width="410" id="mMyCommVBox" backgroundColor="#FFFFFF" backgroundAlpha="0.1" verticalScrollPolicy="on">
	</mx:VBox>
	<mx:VBox x="467" y="50" height="507" width="505" id="mNewsVBox" verticalScrollPolicy="on" backgroundColor="#FFFFFF" backgroundAlpha="0.1">
	</mx:VBox>
	<mx:Canvas x="408" y="537" width="20" height="20" backgroundImage="@Embed(source='../images/smallX.png')" click="smallXClickHandler(event)" id="mSmallXLabel" visible="false" toolTip="Close the remove community group feature">
	</mx:Canvas>
</mx:Canvas>
